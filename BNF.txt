<saltoLinea> ::= "\n"
# Números, variables y asignaciones:
<espacio> ::= " " | "\t"
<espaciosEnBlanco> ::= <espacio>* 
# -- Definir los tipos
<flotante> ::= (0.0) |-?0\ . [0-9]* [1-9]+|-?[1-9][0-9]* . ([0-9]*[1-9]+|0)

<entero> ::=  -? [1 - 9] [0 - 9]* | 0

<enteroPositivo> ::= [1 - 9] [0 - 9]* | 0

#-- Las comillas son para indicar que es un simbolo literal terminal.
<booleano> ::=  "true" | "false"    

#-- En teoria esto: '[^']'     Significa, cualquier caracter menos la comilla ()')
<char> ::= '[^']'

<string> ::= #/"([^"\\] |\ \[nrt])*"/ #-- [ntr] es para cosas como \n, \r, \t


<operadorAritmetico> ::= <operadorAritmeticoBase> | <operadorAritmeticoMDM> | <operadorAritmeticoPotencia> #(\+|\-|\*|\/|\/\/|%|\^)  #--[+\ | - | *\ | / | //]

<operadorAritmeticoBase> ::= (\+|\-)

<operadorAritmeticoMDM> ::= (\*|\/|\/\/|%)

<operadorAritmeticoPotencia> ::= (\^) 

<operadorLogico> ::= (@|~|Σ)

<operadorRelacional> ::= <operadorRelacionalMayores> | <operadorRelacionalIgualdad>

<operadorRelacionalMayores> ::= ("<="|">="|"<"|">")

<operadorRelacionalIgualdad> ::= ("=="|"!=")

#-- Alternativa con sin reguex <operador> ::= "+" | "-" | "*" | "/" | "//"

# -- Alternativa con reguex: <operador> ::= /(\+|\-|\*|\/|\/\/)/


<cAsignacion> ::= \=

<id> ::= [a-z A-Z ] [a-z A-Z 0-9 _]*

<delimitador> ::= $

<tipoVar> ::= "int" | "float" | "boolean" | "char" | "string" 

<tipoInput> ::= "int" | "float"

<tipoFuncion> ::= "int" | "float" | "boolean"

# -- Tipos de datos para definir listas.
<tipoLista> ::=  <tipoListaBase> | <tipoListaTamaño>

<tipoListaBase> ::= "int" "[" "]" | "char" "[" "]"

<tipoListaTamaño> ::= "int" "[" <entero> "]" | "char" "[" <entero> "]"

# -- Definir variables.
<var> ::= <varCreacion> | <varCyAsignacion>

<varCreacion> ::= "let" <espacio> <tipoVar> <espacio> <id> <delimitador>

<varAsignacion> ::= <id> <espacio> <cAsignacion> <espacio> <expresion> <delimitador> | <id> <espacio> <funcionLlamada> <delimitador>

<varCyAsignacion> ::= "let" <tipoVar> <id> <cAsignacion> <expresion> <delimitador>



# -- Las exresiones para una variables puden ser: nada mas numeros o caracteres, una operacion aritmetica o de union de caracteres, 
# -- tambien puede ser entrada de teclado o tambien se podria hacer una condicional para saber asignar un valor booleano.
<expresion> ::= <expresionNumerica> | <expresionAritmetica> | <expresionChar> | <expresionBooleana> | <expresionLogica> | <expresionRelacional>

#-- Def de expresiones basicos.
<expresionNumerica> ::= <entero> | <flotante> | <id> | <obtenerElementoLista> | <funcionLlamada> | <input>

<expresionChar> ::= <char> | <id>

<expresionString> ::= <string> | <id>

<expresionBooleana> ::= <booleano> | <id>


# -- Def de operaciones de expresiones, Construccion con orden de precedencia.
#<expresionAritmetica> ::= <expresionNumerica> | \( <expresionAritmetica> \) | <expresionAritmetica> <operadorAritmetico> <expresionAritmetica>

# -- La expresion aritmetica base.
<expresionAritmetica> ::= <expresionSuma>

# -- La base se puede formar por una operacion base o por una de prodcutos.
<expresionSuma> ::= <expresionSuma> <operadorAritmeticoBase> <expresionProducto>
                 | <expresionProducto>

# -- La de prodcuto esta conformada por la de productos o la de potencia.
<expresionProducto> ::= <expresionProducto> <operadorAritmeticoMDM> <expresionPotencia> #("*" | "/" | "//" | "%")
                      | <expresionPotencia>

# -- La de potencia esta formada por potencia o una de factor.
<expresionPotencia> ::= <expresionFactor> <operadorAritmeticoPotencia> <expresionPotencia>
                      | <expresionFactor>

# -- La de factor es el termino base para cualquier expresio aritmetia 
<expresionFactor> ::= "є" <expresionAritmetica> "э"
                    | <expresionNumerica>
                    | <expresionAritmeticaUnaria>
                    | <expresionAritmeticaUnariaNegativa>


# --Posfija
<expresionAritmeticaUnaria> ::= <id> "++" | <id> "--"

# -- Esto es basicamente para volver un valor negativo, por ejemplo: - 5 | - 3.34 | - x
<expresionAritmeticaUnariaNegativa> ::= "-" <expresionNumerica>


#<expresionLiteral> ::= <expresionLiteral> \+ <expresionLiteral> # Esto no deberia de poder hacerse.


# -- (@|~|Σ)
<expresionLogica> ::= <expresionRelacional>
                    | "є" <expresionLogica> "э"
                    | <expresionLogica> <operadorLogico> <expresionLogica>
                    | <operadorLogico> <expresionLogica>

# -- (==|!=|<=|>=|<|>)
<expresionRelacional> ::= <expresionNumerica> <operadorRelacional> <expresionNumerica> 
                        | <expresionBooleana> <operadorRelacionalIgualdad> <expresionBooleana> 
                        | "є" <expresionRelacional> "э"
                        #-- | <expresionChar> <operadorRelacional> <expresionChar> 
                        #-- | <expresionString> <operadorRelacional> <expresionString>


# Funciones con estructuras de control, bloques y sentencias:
<funciones> ::= <funcion> | <funciones> <funcion>

<funcion> ::= <tipoFuncion> <id> "є" <params> "э" <bloque> | <tipoFuncion> <id> "є" "э" <espaciosEnBlanco> <bloque>

<params> ::= <param> | <params> , <param>

<param> ::= <tipoVar> <id>

<bloque> ::= ¿ <saltoLinea>* <espaciosEnBlanco> <sentencias> <saltoLinea>* \?

<sentencias> ::= <sentencias> <comentarioLinea>?

<sentencias> ::= <sentencia> | <sentencias> <sentencia>

<sentencia> ::= (<var> | <varAsignacion> | <estructruraControl> | <comentarios> | <funcionLlamada> | <return> | <break> | <output> | <modificarElementoLista> | <lista> | <varGlobal>)

<estructruraControl> ::= <decide> | <loop> | <for> # luego se definen abajo

# Decide es de la forma decide of (condicion -> bloque)+ (else -> bloque)? end decide opcional
<decide> ::= "decide of" ( <condicion> "->" <bloque>)+ ("else" <bloque> )? "end" "decide" <delimitador>

<condicion> ::= "є" <expresion> "э"

# Loop es de la forma loop instrucciones... exit wgen condicion$ end loop$
<loop> ::= "loop" <sentencias> <break>? "exit when" <condicion> <delimitador> "end" "loop" <delimitador>

# For es de la forma for <id> step <entero> to/downto <expresion> do bloque
<for> ::= "for" <id> "step" <entero> ("to" | "downto") <expresion> "do" <bloque> <delimitador>

<funcionLlamada> ::= <id> "є" <params> "э" <delimitador> | <id> "є" "э" <delimitador>

<return> ::= "return" <expresion> <delimitador> | "return" <delimitador>

<break> ::= "break" <delimitador>

# El output puede ser de la forma output є expresion э o output є expresion э << output para multiples outputs en una linea.
<output> ::= <output> <delimitador>
<output> ::= <output_inline> | <output> "<<" <output_inline>

<output_inline> ::= "output" "є" <expresion> "э"

# Input puede ser de la forma input є tipo э
<input> ::= "input" "є" <tipoInput> "э" <delimitador>


# Variables globales:

<varGlobal> ::= <varGlobalAsignacion> | <varGlobalCreacion>
<varGlobalAsignacion> ::= "global" <tipoVar> <id> <cAsignacion> <expresion> <delimitador>
<varGlobalCreacion> ::= "global" <tipoVar> <id> <delimitador>

# Tipos de variables enteras, flotantes, booleanas, caracteres, cadenas de caracteres y arreglos:

# >>> Arreglos tipo char y entero; get y update de elementos, ser usados en expresiones y permitir creacion con asignacion:

# Declarar listas

<lista> ::= <declaracionLista> | <creacionYAsignacionLista>

# -- Declarar listas de forma basica
<declaracionLista> ::= "let" <tipoLista> <id> <delimitador>

# -- Esto es para crear una lista con asignacion: let int[] = ¿12,34?
<creacionYAsignacionLista> ::= "let" <tipoLista> <id> "=" "¿" <elementos> "?" "$" #<elementoLista> ("," <elementoLista>)*

#<elementos> ::= <elementoLista>

<elementos> ::= <elementoLista> ("," <elementoLista>)* #<elementos> "," <elementoLista>  

# -- Tipos de datos que puede tener una lista.
<elementoLista> ::= <entero> | <char>

# -- Modificar los elementos de una lista: lista[4] = 23$
<modificarElementoLista> ::= <id> "[" <enteroPositivo> "]" <cAsignacion> <elementoLista> <delimitador> | <id> "[" <id> "]" <cAsignacion> <elementoLista> <delimitador>

# -- Obtener los elementos de una lista: lista[4]    -> Esto puede ser usado en otras expresiones por lo que no lleva delimitador
<obtenerElementoLista> ::= <id> "[" <enteroPositivo> "]" | <id> "[" <id> "]"

# Definición de unicamente un procedimiento main (principal) tipo void sin parametros:

# -- Def de main, puede cambiarse 
<principal> ::= "void" "principal" "є" <expresion> "э" <saltoLinea>* <espaciosEnBlanco> <bloque>

#-- Def de un programa.
<programa> ::= <comentarios>? (<varGlobal>*)? <comentarios>? <funciones>? <comentarios>? <principal> <comentarios>?

# Comentarios en una linea o en multiples lineas:

# -- Todo los comentarios permitidos.
<comentarios> ::= <comentario> | <comentarios> <comentario>

<comentario> ::= <comentarioLinea> | <comentarioBloque>

# -- Se permite cualquier cosa menos lo que serian los saltos de linea.
<comentarioLinea> ::= \|[^\n\r]*

# -- Se permite cualquier caracter o o mas veces y puede o no tener .
<comentarioBloque> ::= ¡[\s\S]*?! #Aqui van los controles de flujo, funcion, loops, numeros, letras, 



